// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=forARegister);

    // A-register
    // load of A-register is d1 => instruction[5]
    Mux(a=true, b=instruction[5], sel=instruction[15], out=loadA);
    ARegister(in=forARegister, load=loadA, out=aRegOut, out[0..14]=addressM);

    // D-register
    // load of D-register is d2 => instruction[4]
    And(a=instruction[4], b=instruction[15], out=loadD);
    DRegister(in=aluOut, load=loadD, out=dRegOut);

    // In the case of a C-instruction, the single a-bit determines
    // whether the ALU will operate on the A register output or on the M input
    // a-bit => instruction[12] 
    Mux16(a=aRegOut, b=inM, sel=instruction[12], out=aluIn);

    ALU(x=dRegOut, y=aluIn, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=aluOut, zr=aluOutZr, ng=aluOutNg);

    // M-register
    // out is load of M-register which is d3 => instruction[3]
    And(a=instruction[3], b=instruction[15], out=writeM);

    // PC section
    Or(a=aluOutNg, b=aluOutZr, out=nonPositive);
    Not(in=nonPositive, out=positive);

    // The three j-bits are used to for branching control.
    And(a=instruction[2], b=aluOutNg, out=ngJmpConfirm);
    And(a=instruction[1], b=aluOutZr, out=zrJmpConfirm);
    And(a=instruction[0], b=positive, out=posJmpConfirm);

    Or(a=ngJmpConfirm, b=zrJmpConfirm, out=ngJmpOrZrJmp);
    Or(a=ngJmpOrZrJmp, b=posJmpConfirm, out=jmp);

    Mux(a=false, b=jmp, sel=instruction[15], out=pcLoad);

    PC(in=aRegOut, load=pcLoad, inc=true, reset=reset, out[0..14]=pc);
}